buildscript {
    repositories {
        maven {url 'https://commondatastorage.googleapis.com/maven-repository/'}
        mavenCentral()
    }
    dependencies {
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.10.+'
        classpath 'com.android.tools.build:gradle:0.10.+'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'android-library'
apply plugin: 'maven'

repositories {
    maven {url 'http://commondatastorage.googleapis.com/maven-repository/'}
    mavenCentral()
}

configurations {
    deployerJars
}

dependencies {
    deployerJars 'com.googlesource.gerrit:gs-maven-wagon:3.3.1'
    compile "com.google.guava:guava:14.0.1"
    compile 'com.android.support:support-v4:19+'
    compile 'com.android.support:appcompat-v7:19+'
    compile fileTree(dir: 'libs', include: '*.jar')
}

def baseVersionName = "1.3"

if (project.hasProperty("versionSuffix")) {
    project.ext.versionSuffix = project.versionSuffix
} else {
    project.ext.versionSuffix = "snapshot"
}
if (project.ext.versionSuffix) {
    version = "${baseVersionName}-${project.versionSuffix}"
} else {
    version = "${baseVersionName}"
}
archivesBaseName = "listviewbackport"
group = "com.appunite"

android {
    compileSdkVersion 19
    buildToolsVersion "19.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage'
        xmlOutput file("${reporting.baseDir}/lint/lint-results.xml")
        htmlOutput file("${reporting.baseDir}/lint/lint-results.html")
    }
}

task createPomFile {
    project.ext.pomFile = pom {
        version = project.version
        artifactId = project.archivesBaseName
        groupId = project.group
        project {
            name 'AndroidListViewBackport'
            description 'library allow easy download images to list.'
            url 'http://appunite.com'
            inceptionYear '2012'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                url "https://review.appunite.com/androidlistviewbackport"
                connection "https://review.appunite.com/androidlistviewbackport.git"
            }
            developers {
                developer {
                    name 'Jacek Marchwicki <jacek.marchwicki@gmail.com>'
                }
            }
        }
    }
}

task install(type: Upload, dependsOn: createPomFile) {
    description = "Upload archives to local repository"
    configuration = configurations.archives
    repositories.mavenInstaller {
        pom = project.ext.pomFile
    }
}


Properties props = new Properties()
def credentialsProperties = file("credentials.properties")
if (credentialsProperties.exists()) {
        props.load(new FileInputStream(credentialsProperties))
}

uploadArchives {
    description = "Upload archives to maven repository"
    dependsOn createPomFile
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "gs://maven-repository") {
                authentication(
                userName: "${props.getProperty('mavenUser', null)}" ,
                password: "${props.getProperty('mavenPassword', null)}")
            }
            pom = project.ext.pomFile
        }
    }
}

